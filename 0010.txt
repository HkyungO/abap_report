*&---------------------------------------------------------------------*
*& Report Z070_0010
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
REPORT Z070_0010.

INCLUDE : Z070_0010_TOP,
          Z070_0010_SCR,
          Z070_0010_F01.

INITIALIZATION.
  PERFORM SET_FUNCTION_KEY.

AT SELECTION-SCREEN.
  PERFORM PUSH_FUNCTION_KEY.

AT SELECTION-SCREEN OUTPUT.
  PERFORM : CHOOSE_RB_CHANGE_SCREEN,
            GET_NAME_FROM_ZEMP.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR PA_ZEMP.
  PERFORM SEARCH_ZEMP.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR SO_PID-LOW.
  PERFORM SEARCH_PROJECT_ID USING SO_PID-LOW.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR SO_PID-HIGH.
  PERFORM SEARCH_PROJECT_ID USING SO_PID-HIGH.

*  AT SELECTION-SCREEN ON VALUE-REQUEST FOR SO_PERI-LOW.
*  PERFORM SEARCH_YEAR_MONTH CHANGING SO_PERI-LOW.

*&---------------------------------------------------------------------*
*&  Include           Z070_0010_TOP
*&---------------------------------------------------------------------*
TABLES : ZEDUT070_0100, SSCRFIELDS.

DATA : GS_DYNTXT TYPE SMP_DYNTXT.

"SEARCH HELP 목적 ITAB.
DATA : BEGIN OF GS_ZEMP,
         ZEMP   TYPE ZEDUT070_0010-ZEMP,      "사원번호.
         ZNAME1 TYPE ZEDUT070_0010-ZNAME1,    "사원명.
       END OF GS_ZEMP,
       GT_ZEMP LIKE TABLE OF GS_ZEMP,

       BEGIN OF GS_ZPROJ,
         ZPROJ  TYPE ZEDUT070_0020-ZPROJ,     "프로젝트 ID.
         ZNAME2 TYPE ZEDUT070_0020-ZNAME2,    "프로젝트명.
       END OF GS_ZPROJ,
       GT_ZPROJ LIKE TABLE OF GS_ZPROJ.

DEFINE _SUBRC_NE_0_EXIT.
  IF SY-SUBRC <> 0.
    "(MESSAGE) 관리자에게 문의해주세요.
    MESSAGE &1 && ` ` && TEXT-T03 TYPE 'S' DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.
END-OF-DEFINITION.

*&---------------------------------------------------------------------*
*&  Include           Z070_0010_SCR
*&---------------------------------------------------------------------*

"선택조건.
SELECTION-SCREEN BEGIN OF BLOCK B1 WITH FRAME TITLE TEXT-T01.
SELECTION-SCREEN BEGIN OF LINE.
"사번기준.
PARAMETERS RD_EMP RADIOBUTTON GROUP RB1 USER-COMMAND UCOMM DEFAULT 'X'.
SELECTION-SCREEN COMMENT (15) FOR FIELD RD_EMP.
"프로젝트 기준.
PARAMETERS RD_PJ RADIOBUTTON GROUP RB1.
SELECTION-SCREEN COMMENT 25(15) FOR FIELD RD_PJ.
"기간기준.
PARAMETERS RD_DATE RADIOBUTTON GROUP RB1.
SELECTION-SCREEN COMMENT 45(10) FOR FIELD RD_DATE.

SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK B1.

"조회조건.
SELECTION-SCREEN BEGIN OF BLOCK B2 WITH FRAME TITLE TEXT-T02.
"사원번호.
PARAMETERS PA_ZEMP TYPE ZEDUT070_0010-ZEMP MODIF ID EMP MEMORY ID ZPA_EMP.
SELECTION-SCREEN COMMENT 58(10) GV_ZNAME MODIF ID EMP.
"프로젝트 ID.
SELECT-OPTIONS SO_PID  FOR ZEDUT070_0100-ZPROJ MODIF ID PID.
"프로젝트 기간.
SELECT-OPTIONS SO_PERI FOR ZEDUT070_0100-ZPERIOD_S MODIF ID PER.

SELECTION-SCREEN SKIP 1.
"현재 진행중인 프로젝트만 조회.
PARAMETERS CH_ING TYPE C AS CHECKBOX.
SELECTION-SCREEN END OF BLOCK B2.

SELECTION-SCREEN FUNCTION KEY 1.

*&---------------------------------------------------------------------*
*&  Include           Z070_0010_F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  SET_FUNCTION_KEY
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM SET_FUNCTION_KEY .

  GS_DYNTXT-ICON_ID     = ICON_CHANGE_TEXT.
  "사원기준정보변경.
  GS_DYNTXT-ICON_TEXT   = TEXT-F01.
  "사원별 정보 변경이 가능한 창으로 연결됩니다.
  GS_DYNTXT-QUICKINFO   = TEXT-F02.
  SSCRFIELDS-FUNCTXT_01 = GS_DYNTXT.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  CHOOSE_RB_CHANGE_SCREEN
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM CHOOSE_RB_CHANGE_SCREEN .

  LOOP AT SCREEN.
    CASE ABAP_TRUE.
        "사번기준 선택 시 ①프로젝트 ID, ②프로젝트 기간 UNACTIVE.
      WHEN RD_EMP.
        "첫 LOOP에서만 CLEAR.
        IF SCREEN-NAME = 'SSCRFIELDS-UCOMM'.
          CLEAR GV_ZNAME.
        ENDIF.
        IF SCREEN-GROUP1 = 'PID'
        OR SCREEN-GROUP1 = 'PER'.
          SCREEN-ACTIVE = 0.
        ENDIF.
        "프로젝트 기준 선택 시 ①사원번호, ②프로젝트 기간 UNACTIVE.
      WHEN RD_PJ.
        IF SCREEN-GROUP1 = 'EMP'
        OR SCREEN-GROUP1 = 'PER'.
          SCREEN-ACTIVE = 0.
        ENDIF.
        "기간 기준 선택 시 ①사원번호, ②프로젝트 ID UNACTIVE.
      WHEN RD_DATE.
        IF SCREEN-GROUP1 = 'EMP'
        OR SCREEN-GROUP1 = 'PID'.
          SCREEN-ACTIVE = 0.
        ENDIF.
    ENDCASE.
    MODIFY SCREEN.
  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  PUSH_FUNCTION_KEY
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM PUSH_FUNCTION_KEY .

  IF SSCRFIELDS-UCOMM = 'FC01'.
    "T-CODE 불일치 시 예외 처리 불가(Non-Handleable Exceptions)→선 체크.
    SELECT SINGLE SPRSL
      FROM TSTCT
      INTO SY-LANGU
     WHERE SPRSL = SY-LANGU
       AND TCODE = 'ZEDUV070_0010'.
    "연결 중 문제가 발생했습니다. 관리자에게 문의해주세요.
    _SUBRC_NE_0_EXIT : TEXT-E02.

    "사원기준정보_유지보수VIEW.
    CALL TRANSACTION 'ZEDUV070_0010'.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  GET_NAME_FROM_ZEMP
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM GET_NAME_FROM_ZEMP .

  "사원번호 입력 시 사원명 출력.
  IF PA_ZEMP IS NOT INITIAL.
    SELECT SINGLE ZNAME1
      FROM ZEDUT070_0010
      INTO GV_ZNAME
     WHERE ZEMP = PA_ZEMP.
    IF SY-SUBRC <> 0.
      "존재하지 않는 사원번호입니다.
      MESSAGE TEXT-E01 TYPE 'S' DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SEARCH_PROJECT_ID
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_SO_PID_LOW  text
*----------------------------------------------------------------------*
FORM SEARCH_PROJECT_ID  USING    VALUE(PV_PID).

  SELECT ZPROJ
         ZNAME2
    FROM ZEDUT070_0020
    INTO TABLE GT_ZPROJ.
  "도움말 데이터가 없습니다. 관리자에게 문의해주세요.
  _SUBRC_NE_0_EXIT : TEXT-E04.

  "프로젝트 ID - F4.
  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      RETFIELD    = 'ZPROJ'
      DYNPPROG    = SY-REPID
      DYNPNR      = SY-DYNNR
      DYNPROFIELD = 'P_SO_PID'
      VALUE_ORG   = 'S'
    TABLES
      VALUE_TAB   = GT_ZPROJ.
  "도움말 실행 중 문제가 발생했습니다. 관리자에게 문의해주세요.
  _SUBRC_NE_0_EXIT : TEXT-E03.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SEARCH_ZEMP
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM SEARCH_ZEMP .

  SELECT ZEMP
         ZNAME1
    FROM ZEDUT070_0010
    INTO TABLE GT_ZEMP.

  "사원번호 - F4.
  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      RETFIELD    = 'ZEMP'
      DYNPPROG    = SY-REPID
      DYNPNR      = SY-DYNNR
      DYNPROFIELD = 'PA_ZEMP'
      VALUE_ORG   = 'S'
    TABLES
      VALUE_TAB   = GT_ZEMP.
  "도움말 실행 중 문제가 발생했습니다. 관리자에게 문의해주세요.
  _SUBRC_NE_0_EXIT : TEXT-E03.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SEARCH_YEAR_MONTH
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_SO_DATE_HIGH  text
*----------------------------------------------------------------------*
FORM SEARCH_YEAR_MONTH  CHANGING    PV_PERI.

DATA LV_RET TYPE SY-SUBRC.

  CALL FUNCTION 'POPUP_TO_SELECT_MONTH'
    EXPORTING
      ACTUAL_MONTH               = SY-DATUM(6)
      FACTORY_CALENDAR           = SPACE            " Factory Calendar
*      HOLIDAY_CALENDAR           = SPACE            " Public Holiday Calendar
      LANGUAGE                   = SY-LANGU
      START_COLUMN               = 8
      START_ROW                  = 5
    IMPORTING
      SELECTED_MONTH             = PV_PERI
      RETURN_CODE                = LV_RET                 " 미선택시 4 반환
    EXCEPTIONS
      FACTORY_CALENDAR_NOT_FOUND = 1
      HOLIDAY_CALENDAR_NOT_FOUND = 2
      MONTH_NOT_FOUND            = 3
      OTHERS                     = 4
    .
  IF SY-SUBRC <> 0.
*   MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*     WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

ENDFORM.